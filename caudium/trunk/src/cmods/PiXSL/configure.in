AC_INIT(sablot.c)
AC_CONFIG_HEADER(sablot_config.h)

CAUDIUM_MODULE_INIT()

AC_PROG_INSTALL

dnl Solaris have math functions in libm
dnl so this is needed to linking libsablot
AC_CHECK_LIB(m, ceil)
AC_CHECK_LIB(m, floor)

AC_CHECK_HEADERS( sablot.h )

dnl No need to continue unless the header is found...
if test "$ac_cv_header_sablot_h" = "yes"; then
  unset ac_cv_lib_sablot_SablotProcess
  AC_CHECK_LIB(sablot, SablotProcess,
  [
    AC_DEFINE(HAVE_LIBSABLOT)
	LIBS="$LIBS -lsablot"
  ], [
    unset ac_cv_lib_sablot_SablotProcess
    AC_CHECK_LIB(sablot, SablotProcess,
    [
	  AC_DEFINE(HAVE_LIBSABLOT)
	  LIBS="$LIBS -lsablot -lexpat"
    ], [
      unset ac_cv_lib_sablot_SablotProcess
      AC_CHECK_LIB(sablot, SablotProcess,
      [
		AC_DEFINE(HAVE_LIBSABLOT)
		LIBS="$LIBS -lsablot -lxmlparse -lxmltok"
      ],[], -lxmlparse -lxmltok)
    ], -lexpat)
  ])
fi

if test "$ac_cv_header_sablot_h" != "yes" -o \
	    "$ac_cv_lib_sablot_SablotProcess" != "yes"; then
cat << EOF
*************************************************************
**** The Sablotron XSLT processing library couldn't be found.
**** PiXSL will not be built. You can get Sablotron from
**** http://www.gingerall.com/
****
**** Install the library and remove config.cache in the base
**** build directory and rerun configure to get XSLT support.
****
**** To build Caudium without XSLT support run configure with
**** the --without-PiXSL flag.
*************************************************************
EOF
exit 1
fi

AC_OUTPUT(Makefile.pre pixsl, [make -f Makefile.pre depend])


dnl ## Local Variables:
dnl ## tab-width: 4
dnl ## End:
	
